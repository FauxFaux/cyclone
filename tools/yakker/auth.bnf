      auth-scheme    = token
.
      auth-param     = token "=" ( token | quoted-string )
.
      challenge   = auth-scheme 1*SP 1#auth-param
.
      realm       = "realm" "=" realm-value
.
      realm-value = quoted-string
.
;      credentials = "Basic" basic-credentials
      credentials = "Basic" basic-credentials
                  | "Digest" digest-response
.
      basic-credentials = base64-user-pass
.
      base64-user-pass  = <base64 [4] encoding of user-pass,
                       except not limited to 76 char/line>
.
      user-pass   = userid ":" password
.
      userid      = *<TEXT excluding ":">
.
      password    = *TEXT
.
      digest-challenge  = 1#( realm | [ domain ] | nonce |
                          [ opaque ] |[ stale ] | [ algorithm ] |
                          [ qop-options ] | [auth-param] )


.
      domain            = "domain" "=" <"> URI ( 1*SP URI ) <">
.
      URI               = absoluteURI | abs_path
.
      nonce             = "nonce" "=" nonce-value
.
      nonce-value       = quoted-string
.
      opaque            = "opaque" "=" quoted-string
.
      stale             = "stale" "=" ( "true" | "false" )
.
      algorithm         = "algorithm" "=" ( "MD5" | "MD5-sess" |
                           token )
.
      qop-options       = "qop" "=" <"> 1#qop-value <">
.
      qop-value         = "auth" | "auth-int" | token
.
;       credentials      = "Digest" digest-response
;.
       digest-response  = 1#( username | realm | nonce | digest-uri
                       | response | [ algorithm ] | [cnonce] |
                       [opaque] | [message-qop] |
                           [nonce-count]  | [auth-param] )

.
       username         = "username" "=" username-value
.
       username-value   = quoted-string
.
       digest-uri       = "uri" "=" digest-uri-value
.
;       digest-uri-value = request-uri   ; As specified by HTTP/1.1
       digest-uri-value = Request-URI   ; As specified by HTTP/1.1
.
       message-qop      = "qop" "=" qop-value
.
       cnonce           = "cnonce" "=" cnonce-value
.
       cnonce-value     = nonce-value
.
       nonce-count      = "nc" "=" nc-value
.
       nc-value         = 8LHEX
.
       response         = "response" "=" request-digest
.
       request-digest = <"> 32LHEX <">
.
       LHEX             =  "0" | "1" | "2" | "3" |
                           "4" | "5" | "6" | "7" |
                           "8" | "9" | "a" | "b" |
                           "c" | "d" | "e" | "f"
.
        AuthenticationInfo = "Authentication-Info" ":" auth-info
.
        auth-info          = 1#(nextnonce | [ message-qop ]
                               | [ response-auth ] | [ cnonce ]
                               | [nonce-count] )
.
        nextnonce          = "nextnonce" "=" nonce-value
.
        response-auth      = "rspauth" "=" response-digest
.
        response-digest    = <"> *LHEX <">
.
