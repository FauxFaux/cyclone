NO-WS-CTL       =       %d1-8 /         ; US-ASCII control characters
                        %d11 /          ;  that do not include the
                        %d12 /          ;  carriage return, line feed,
                        %d14-31 /       ;  and white space characters
                        %d127

.
text            =       %d1-9 /         ; Characters excluding CR and LF
                        %d11 /
                        %d12 /
                        %d14-127 /
                        obs-text

.
specials        =       "(" / ")" /     ; Special characters used in
                        "<" / ">" /     ;  other parts of the syntax
                        "[" / "]" /
                        ":" / ";" /
                        "@" / "\" /
                        "," / "." /
                        DQUOTE

.
quoted-pair     =       ("\" text) / obs-qp
.
FWS             =       ([*WSP CRLF] 1*WSP) /   ; Folding white space
                        obs-FWS

.
ctext           =       NO-WS-CTL /     ; Non white space controls
                        %d33-39 /       ; The rest of the US-ASCII
                        %d42-91 /       ;  characters not including "(",
                        %d93-126        ;  ")", or "\"
.
ccontent        =       ctext / quoted-pair / comment

.
comment         =       "(" *([FWS] ccontent) [FWS] ")"

.
CFWS            =       *([FWS] comment) (([FWS] comment) / FWS)

.
atext           =       ALPHA / DIGIT / ; Any character except controls,
                        "!" / "#" /     ;  SP, and specials.
                        "$" / "%" /     ;  Used for atoms
                        "&" / "'" /
                        "*" / "+" /
                        "-" / "/" /
                        "=" / "?" /
                        "^" / "_" /
                        "`" / "{" /
                        "|" / "}" /
                        "~"

.
atom            =       [CFWS] 1*atext [CFWS]

.
dot-atom        =       [CFWS] dot-atom-text [CFWS]

.
dot-atom-text   =       1*atext *("." 1*atext)

.
qtext           =       NO-WS-CTL /     ; Non white space controls

                        %d33 /          ; The rest of the US-ASCII
                        %d35-91 /       ;  characters not including "\"
                        %d93-126        ;  or the quote character

.
qcontent        =       qtext / quoted-pair

.
quoted-string   =       [CFWS]
                        DQUOTE *([FWS] qcontent) [FWS] DQUOTE
                        [CFWS]
.
word            =       atom / quoted-string

.
phrase          =       1*word / obs-phrase

.
utext           =       NO-WS-CTL /     ; Non white space controls
                        %d33-126 /      ; The rest of US-ASCII
                        obs-utext

.
unstructured    =       *([FWS] utext) [FWS]

.
date-time       =       [ day-of-week "," ] date FWS time [CFWS]

.
day-of-week     =       ([FWS] day-name) / obs-day-of-week

.
day-name        =       "Mon" / "Tue" / "Wed" / "Thu" /
                        "Fri" / "Sat" / "Sun"

.
date            =       day month year

.
year            =       4*DIGIT / obs-year

.
month           =       (FWS month-name FWS) / obs-month

.
month-name      =       "Jan" / "Feb" / "Mar" / "Apr" /
                        "May" / "Jun" / "Jul" / "Aug" /
                        "Sep" / "Oct" / "Nov" / "Dec"

.
day             =       ([FWS] 1*2DIGIT) / obs-day

.
time            =       time-of-day FWS zone

.
time-of-day     =       hour ":" minute [ ":" second ]

.
hour            =       2DIGIT / obs-hour

.
minute          =       2DIGIT / obs-minute

.
second          =       2DIGIT / obs-second

.
zone            =       (( "+" / "-" ) 4DIGIT) / obs-zone

.
address         =       mailbox / group

.
mailbox         =       name-addr / addr-spec

.
name-addr       =       [display-name] angle-addr

.
angle-addr      =       [CFWS] "<" addr-spec ">" [CFWS] / obs-angle-addr

.
group           =       display-name ":" [mailbox-list / CFWS] ";"
                        [CFWS]

.
display-name    =       phrase

.
mailbox-list    =       (mailbox *("," mailbox)) / obs-mbox-list

.
address-list    =       (address *("," address)) / obs-addr-list
.
addr-spec       =       local-part "@" domain

.
local-part      =       dot-atom / quoted-string / obs-local-part

.
domain          =       dot-atom / domain-literal / obs-domain

.
domain-literal  =       [CFWS] "[" *([FWS] dcontent) [FWS] "]" [CFWS]

.
dcontent        =       dtext / quoted-pair

.
dtext           =       NO-WS-CTL /     ; Non white space controls

                        %d33-90 /       ; The rest of the US-ASCII
                        %d94-126        ;  characters not including "[",
                                        ;  "]", or "\"
.
message         =       (fields / obs-fields)
                        [CRLF body]

.
body            =       *(*998text CRLF) *998text
.
fields          =       *(trace
                          *(resent-date /
                           resent-from /
                           resent-sender /
                           resent-to /
                           resent-cc /
                           resent-bcc /
                           resent-msg-id))
                        *(orig-date /
                        from /
                        sender /
                        reply-to /
                        to /
                        cc /
                        bcc /
                        message-id /
                        in-reply-to /
                        references /
                        subject /
                        comments /
                        keywords /
                        optional-field)
.
orig-date       =       "Date:" date-time CRLF

.
from            =       "From:" mailbox-list CRLF

.
sender          =       "Sender:" mailbox CRLF

.
reply-to        =       "Reply-To:" address-list CRLF
.
to              =       "To:" address-list CRLF

.
cc              =       "Cc:" address-list CRLF

.
bcc             =       "Bcc:" (address-list / [CFWS]) CRLF

.
message-id      =       "Message-ID:" msg-id CRLF

.
in-reply-to     =       "In-Reply-To:" 1*msg-id CRLF

.
references      =       "References:" 1*msg-id CRLF

.
msg-id          =       [CFWS] "<" id-left "@" id-right ">" [CFWS]

.
id-left         =       dot-atom-text / no-fold-quote / obs-id-left

.
id-right        =       dot-atom-text / no-fold-literal / obs-id-right

.
no-fold-quote   =       DQUOTE *(qtext / quoted-pair) DQUOTE

.
no-fold-literal =       "[" *(dtext / quoted-pair) "]"

.
subject         =       "Subject:" unstructured CRLF

.
comments        =       "Comments:" unstructured CRLF

.
keywords        =       "Keywords:" phrase *("," phrase) CRLF
.
resent-date     =       "Resent-Date:" date-time CRLF

.
resent-from     =       "Resent-From:" mailbox-list CRLF

.
resent-sender   =       "Resent-Sender:" mailbox CRLF

.
resent-to       =       "Resent-To:" address-list CRLF

.
resent-cc       =       "Resent-Cc:" address-list CRLF

.
resent-bcc      =       "Resent-Bcc:" (address-list / [CFWS]) CRLF

.
resent-msg-id   =       "Resent-Message-ID:" msg-id CRLF
.
trace           =       [return]
                        1*received

.
return          =       "Return-Path:" path CRLF

.
path            =       ([CFWS] "<" ([CFWS] / addr-spec) ">" [CFWS]) /
                        obs-path

.
received        =       "Received:" name-val-list ";" date-time CRLF

.
name-val-list   =       [CFWS] [name-val-pair *(CFWS name-val-pair)]

.
name-val-pair   =       item-name CFWS item-value

.
item-name       =       ALPHA *(["-"] (ALPHA / DIGIT))

.
item-value      =       1*angle-addr / addr-spec /
                         atom / domain / msg-id
.
optional-field  =       field-name ":" unstructured CRLF

.
field-name      =       1*ftext

.
ftext           =       %d33-57 /               ; Any character except
                        %d59-126                ;  controls, SP, and
                                                ;  ":".
.
obs-qp          =       "\" (%d0-127)

.
obs-text        =       *LF *CR *(obs-char *LF *CR)

.
obs-char        =       %d0-9 / %d11 /          ; %d0-127 except CR and
                        %d12 / %d14-127         ;  LF

.
obs-utext       =       obs-text

.
obs-phrase      =       word *(word / "." / CFWS)

.
obs-phrase-list =       phrase / 1*([phrase] [CFWS] "," [CFWS]) [phrase]

.
   obs-FWS         =       1*WSP *(CRLF 1*WSP)

.
obs-day-of-week =       [CFWS] day-name [CFWS]

.
obs-year        =       [CFWS] 2*DIGIT [CFWS]

.
obs-month       =       CFWS month-name CFWS

.
obs-day         =       [CFWS] 1*2DIGIT [CFWS]

.
obs-hour        =       [CFWS] 2DIGIT [CFWS]

.
obs-minute      =       [CFWS] 2DIGIT [CFWS]

.
obs-second      =       [CFWS] 2DIGIT [CFWS]

.
obs-zone        =       "UT" / "GMT" /          ; Universal Time
                                                ; North American UT
                                                ; offsets
                        "EST" / "EDT" /         ; Eastern:  - 5/ - 4
                        "CST" / "CDT" /         ; Central:  - 6/ - 5
                        "MST" / "MDT" /         ; Mountain: - 7/ - 6
                        "PST" / "PDT" /         ; Pacific:  - 8/ - 7

                        %d65-73 /               ; Military zones - "A"
                        %d75-90 /               ; through "I" and "K"
                        %d97-105 /              ; through "Z", both
                        %d107-122               ; upper and lower case

.
obs-angle-addr  =       [CFWS] "<" [obs-route] addr-spec ">" [CFWS]

.
obs-route       =       [CFWS] obs-domain-list ":" [CFWS]

.
obs-domain-list =       "@" domain *(*(CFWS / "," ) [CFWS] "@" domain)

.
obs-local-part  =       word *("." word)

.
obs-domain      =       atom *("." atom)

.
obs-mbox-list   =       1*([mailbox] [CFWS] "," [CFWS]) [mailbox]

.
obs-addr-list   =       1*([address] [CFWS] "," [CFWS]) [address]

.
obs-fields      =       *(obs-return /
                        obs-received /
                        obs-orig-date /
                        obs-from /
                        obs-sender /
                        obs-reply-to /
                        obs-to /
                        obs-cc /
                        obs-bcc /
                        obs-message-id /
                        obs-in-reply-to /
                        obs-references /
                        obs-subject /
                        obs-comments /
                        obs-keywords /
                        obs-resent-date /
                        obs-resent-from /
                        obs-resent-send /
                        obs-resent-rply /
                        obs-resent-to /
                        obs-resent-cc /
                        obs-resent-bcc /
                        obs-resent-mid /
                        obs-optional)
.
obs-orig-date   =       "Date" *WSP ":" date-time CRLF
.
obs-from        =       "From" *WSP ":" mailbox-list CRLF

.
obs-sender      =       "Sender" *WSP ":" mailbox CRLF

.
obs-reply-to    =       "Reply-To" *WSP ":" mailbox-list CRLF

.
obs-to          =       "To" *WSP ":" address-list CRLF

.
obs-cc          =       "Cc" *WSP ":" address-list CRLF

.
obs-bcc         =       "Bcc" *WSP ":" (address-list / [CFWS]) CRLF
.
obs-message-id  =       "Message-ID" *WSP ":" msg-id CRLF

.
obs-in-reply-to =       "In-Reply-To" *WSP ":" *(phrase / msg-id) CRLF

.
obs-references  =       "References" *WSP ":" *(phrase / msg-id) CRLF

.
obs-id-left     =       local-part

.
obs-id-right    =       domain
.
obs-subject     =       "Subject" *WSP ":" unstructured CRLF

.
obs-comments    =       "Comments" *WSP ":" unstructured CRLF

.
obs-keywords    =       "Keywords" *WSP ":" obs-phrase-list CRLF
.
obs-resent-from =       "Resent-From" *WSP ":" mailbox-list CRLF

.
obs-resent-send =       "Resent-Sender" *WSP ":" mailbox CRLF
.
obs-resent-date =       "Resent-Date" *WSP ":" date-time CRLF

.
obs-resent-to   =       "Resent-To" *WSP ":" address-list CRLF

.
obs-resent-cc   =       "Resent-Cc" *WSP ":" address-list CRLF

.
obs-resent-bcc  =       "Resent-Bcc" *WSP ":"
                         (address-list / [CFWS]) CRLF

.
obs-resent-mid  =       "Resent-Message-ID" *WSP ":" msg-id CRLF

.
obs-resent-rply =       "Resent-Reply-To" *WSP ":" address-list CRLF
.
obs-return      =       "Return-Path" *WSP ":" path CRLF

.
obs-received    =       "Received" *WSP ":" name-val-list CRLF

.
obs-path        =       obs-angle-addr
.
obs-optional    =       field-name *WSP ":" unstructured CRLF
.
