/* Copyright (C) 1999 Lucent Technologies */
/* Excerpted from 'The Practice of Programming' */
/* by Brian W. Kernighan and Rob Pike */
/* Ported to Cyclone by James Cheney, April 2001 */

#include "stdio.h"
#include "string.h"

using Stdio;
using Core;
using String;

int grep(string, FILE@, string);
int match(string, string);
int matchhere(string, string);
int matchstar(int, string, string);

/* grep main: search for regexp in files */
int main(int argc, string? argv)
{
  int i, nmatch;
  FILE *f;
  if (argc < 2)
    fprintf(stderr,"usage: grep regexp [file ...]\n");
  nmatch = 0;
  if (argc == 2) {
    if (grep(argv[1], stdin, null))
      nmatch++;
  } else {
    for (i = 2; i < argc; i++) {
      f = fopen(argv[i], "r");
      if (f == null) {
	fprintf(stderr,"can't open %s:\n", argv[i]);
	continue;
      }
      if (grep(argv[1], f, argc>3 ? argv[i] : null) > 0)
	nmatch++;
      fclose(f);
    }
  }
  return nmatch == 0;
}

/* grep: search for regexp in file */
int grep(string regexp, FILE @f, string name)
{
  int n, nmatch;
  string buf = expand("",BUFSIZ);

  nmatch = 0;
  while (fgets(buf, BUFSIZ, f) != null) {
    n = String::strlen(buf);
    
    if (n > 0 && buf[n-1] == '\n')
      buf[n-1] = '\0';
    if (match(regexp, buf)) {
      nmatch++;
      if (name != null)
	printf("%s:", name);
      printf("%s\n", buf);
    }
  }
  return nmatch;
}

/* matchhere: search for regexp at beginning of text */
int matchhere(string regexp, string text)
{
  if (strlen(regexp) == 0)
    return 1;
  /*  if (regexp[1] == '*')
      return matchstar(regexp[0], regexp+2, text);*/
  if (regexp[0] == '$' && regexp[1] == '\0')
    return *text == '\0';
  if (*text!='\0' && (regexp[0]=='.' || regexp[0]==*text))
    return matchhere(regexp+1, text+1);
  return 0;
}

/* match: search for regexp anywhere in text */
int match(string regexp, string text)
{
  if (regexp[0] == '^')
    return matchhere(regexp+1, text);
  do {	/* must look even if string is empty */
    if (matchhere(regexp, text))
      return 1;
  } while (*text++ != '\0');
  return 0;
}

/* matchstar: search for c*regexp at beginning of text */
int matchstar(int c, string regexp, string text)
{
  do {	/* a * matches zero or more instances */
    if (matchhere(regexp, text))
      return 1;
  } while (*text != '\0' && (*text++ == c || c == '.'));
  return 0;
}
